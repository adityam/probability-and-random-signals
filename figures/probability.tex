\usepath[/home/adityam/projects/presentations]

\enablemode[lucida]
\environment env-fonts.tex
\setupbodyfont[slidefont,16pt]

\usemodule[asciimath]


\startMPdefinitions
   vardef label_omega(expr n) =
      "$Ï‰_{" & decimal n & "}$"
   enddef;
      
   def ProbabilitySpace = applyparameters "ProbabilitySpace" "do_ProbabilitySpace" enddef ;

   presetparameters "ProbabilitySpace" [
      nx   = 4,      ny   = 1,
      sx   = 1.5cm,  sy   = 1.5cm,
      
      probability = { },
      events = { },

      eventfillcolor = "lightgray",
      eventcolor = "lightgray",


      labelstyle = "\switchtobodyfont[8pt]",

      labels = { },
      label_macro = "label_omega", 
    ];

    vardef do_ProbabilitySpace = 
      image(
        pushparameters "ProbabilitySpace"; 
        textextoffset := 3pt;

        newnumeric sx, sy;
        sx := getparameter "sx";
        sy := getparameter "sy";

        newnumeric nx, ny;
        nx := getparameter "nx";
        ny := getparameter "ny";

        newstring lbl; 
        newnumeric n;

        for j = 1 upto ny:
          for i = 1 upto nx:
             idx := (j-1)*nx + i ;
             draw unitsquare xyscaled (sx, sy) shifted (i*sx, j*sy);

             n := getparametercount "labels";
             lbl := if n > 0  :
                       getparameter "labels" idx
                    else : 
                        scantokens(getparameter "label_macro")(idx)
                    fi;

             label.lrt((getparameter "labelstyle") & lbl, (i*sx, (j+1)*sy));

             draw thefmttext("\asciimath{@s}", getparameter "probability" idx,
                             ((i+0.5)*sx, (j+0.5)*sy) );
          endfor
        endfor

        n := getparametercount "events";

        newpath event;

        for k = 1 upto n :
           event := (getparameter "events" k) xyscaled (sx,sy) ;
           fill event withcolor getparameter "eventfillcolor" withstacking -1;
           draw event;
        endfor
      )
    enddef;
\stopMPdefinitions


\starttext

\startMPpage[offset=1dk]
  draw ProbabilitySpace[nx=3, ny=2, 
          probability={"2/15", "2/15", "2/15", "2/15", "2/15", "1/3"}];

  currentpicture := currentpicture scaled 1.5;
\stopMPpage

\startMPpage[offset=1dk]
  newpath A;
  A := (2, 1) -- (4, 1) -- (4, 1.9) -- (2.9, 1.9) -- (2.9, 3) -- (2,3) -- cycle;  
  A := A cornered (0.2) blownup (-0.1,-0.1) randomized 0.03;

  draw ProbabilitySpace[nx=3, ny=2, 
          probability={"2/15", "2/15", "2/15", "2/15", "2/15", "1/3"},
          events = {A},
          ];

  currentpicture := currentpicture scaled 1.5;
\stopMPpage

\startMPpage[offset=1dk]
  draw ProbabilitySpace[nx=2, ny=2, labels = { "HH", "HT", "TH", "TT"},
          probability={"1/4", "1/4", "1/4", "1/4"}];

  currentpicture := currentpicture scaled 1.5;
\stopMPpage

\startMPpage[offset=1dk]
  draw ProbabilitySpace[nx=3, ny=1, labels = { "$0$", "$1$", "$2$" },
          probability={"1/4", "1/2", "1/4"}];

  currentpicture := currentpicture scaled 1.5;
\stopMPpage

\startMPpage[offset=1dk]
  save A; path A[];

  A[1] := (1,1)--(2,1)--(2,2)--(1,2) -- cycle;
  A[3] := A[1] shifted (1,1);

  for i = 1, 3 :
    A[i] := A[i] cornered (0.2) blownup (-0.1,-0.1) randomized 0.03;
  endfor

  A[2] := (2.1,1.1) .. tension 1.5 .. (2.9, 1.1) .. tension 1.5 .. (2.9, 1.9) .. (2.2, 1.9) ---
          (1.9, 2.2) .. (1.9, 2.9) .. tension 1.5 .. (1.1, 2.9) .. tension 1.5 .. (1.1, 2.1) .. 
          (1.8, 2.1) --- (2.1, 1.8) .. cycle;

  A[2] := A[2] randomized 0.03;

  draw ProbabilitySpace[nx=2, ny=2, labels = { "HH", "HT", "TH", "TT"},
          probability={"1/4", "1/4", "1/4", "1/4"},
          events = {A[1], A[2], A[3]},
          ];

  currentpicture := currentpicture scaled 1.5;
\stopMPpage

\stoptext

